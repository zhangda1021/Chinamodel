$TITLE  USREP --- Reference scenario file

$setglobal BAU BAU_yes 

*--------------------------------------------------------------------------
*	NUMBER OF PERIODS (2006=1, 2010=2, 2015=3,...,2050=10,...,2100=20)

PARAMETER	nper;
nper = 24;


$exit
*--------------------------------------------------------------------------
*	RESOURCE DEPLETION: 
*	1. set price path exogenously (ACTIVATED IF depper=nper+1)
*	2. depletion model (ACTIVATED IF depper=>2)

PARAMETER	depper(fif);
depper(fif) = 2;

*--------------------------------------------------------------------------
*	Activate BACKSTOP TECHNOLOGIES:

BACKSTOP(T) = YES$(ORD(T) GE 1);
ACTIVE(BT,R) = NO;

*MDV
*$ontext
*--------------------------------------------------------------------------
*       VINTAGING:

*       Specify the benchmark conditions:

PARAMETER CLAY_SHR(G,R)   Benchmark fraction of capital stock which is old
	  VINT_SHR	  Share of benchmark production by vintage
	  idkd		  Identifier for benchmark sectoral capital demand;

idkd(r,g) = kd0(r,g);

CLAY_SHR(G,R) = 0;
*	Extant capital only for sectors which have positive 
*	benchmark capital demand:
CLAY_SHR(G,R)$idkd(r,g) = THETA0(G,R); 
alias(V,VV);
VINT_SHR(V,R) = SRVSHR(R)**ORD(V) / SUM(VV,SRVSHR(R)**ORD(VV));

*       Adopt the putty clay assumption for the following sectors:

SET PCGOODS(G) / AGR, ELE, EIS, OTH, TRN /;

VINTG(PCGOODS,R) = YES;
VINTGBS(VBT,R) = YES;

*VINTGBS(VBT,R) = no;

*	Initialize coefficients for vintaged production:

V_DE(R,E,G,V)$(VINTG(G,R) and vomout(r,g)) 
		= (vdfm(r,e,g)+sum(trd,vifm(r,e,trd,g)))/vomout(r,g);
V_DF(R,G,V)$(VINTG(G,R) and vomout(r,g))
*		= sum(h,ffacth(r,h,g)) /vomout(r,g);
		= rr0(r,g) /vomout(r,g);
V_DK(R,G,V)$(VINTG(G,R) and vomout(r,g))
	        = kd0(r,g) /vomout(r,g);
V_DL(R,G,V)$(VINTG(G,R) and vomout(r,g))
	        = vfm(r,"empl",g) /vomout(r,g);
V_K(G,V,R)$VINTG(G,R)    = CLAY_SHR(G,R) * kd0(r,g) * VINT_SHR(V,R);
V_KH(G,H,V,R)$VINTG(G,R) = kapital(r,h)/sum(hh,kapital(r,hh)) * V_K(G,V,R);
*	Set small coefficients to zero to avoid numerical issues: 
V_KH(G,H,V,R)$(VINTG(G,R)$(V_KH(G,H,V,R) lt 1e-5)) = 0; 

VB_K(VBT,V,R)$VINTGBS(VBT,R) 	= 1e-4;
VB_KM(VBT,V,R)$VINTGBS(VBT,R) 	= 1e-4;

*       Adjust the new vintage capital stock:

PARAMETER AKAPITAL   Aggregate capital;

AKAPITAL(R) = sum(h,kapital(r,h)) - SUM(G$VINTG(G,R), SUM(V,V_K(G,V,R))) 
	- SUM(VBT$VINTGBS(VBT,R), SUM(V, VB_K(VBT,V,R)))
	- SUM(VBT$VINTGBS(VBT,R), SUM(V, VB_KM(VBT,V,R)));

*kapital(r,h) = kapital(r,h)/sum(hh,kapital(r,hh)) * akapital(r);
kapital(r,h) = kapital(r,h)-sum((g,v),V_KH(G,H,V,R)$VINTG(G,R));

*       Recalibrate the new vintage capital:

y.l(r,g)$VINTG(G,R) = 1 - CLAY_SHR(G,R);
en.l(r,g)$VINTG(G,R) = 1 - CLAY_SHR(G,R);
dv.l(G,V,R)$VINTG(G,R) = (vdm(r,g)+sum(trd,vxm(r,g,trd))) * CLAY_SHR(G,R) * VINT_SHR(V,R);

*	Adjustment factors for GHG reporting:
parameter 
	adj_ghg		"Adjust benchmark non-CO2 GHG emissions since we adjusted activity levels due to vintaging"
	adj_ghgv	"Adjust benchmark non-CO2 GHG emissions since we adjusted activity levels due to vintaging";
			
adj_ghg(r,g)$vintg(g,r) = 1/(1-CLAY_SHR(G,R));
adj_ghgv(r,g,v)$(((vdm(r,g)+sum(trd,vxm(r,g,trd))) * CLAY_SHR(G,R) * VINT_SHR(V,R))$vintg(g,r))
	 = 1/((vdm(r,g)+sum(trd,vxm(r,g,trd))) * CLAY_SHR(G,R) * VINT_SHR(V,R));


*$offtext

*--------------------------------------------------------------------------
*	RENEWABLE ELECTRICITY STANDARD (RES)

*	We use the RES instrument to force renewables into baseline
*	to match AEO2009 projections. 

*	Activate RES instruments:
sren(r,t)$((ord(t) ge 2) and renshareAEO(r,t)) = no;

*	Set RES target (= electricity generated by REN (EJ) / total electricity (EJ))":

rest(r,t) = renshareAEO(r,t);

*	NB: We want to target physical electricity production from renewables as a share of total electricity
*	    production. The phi parameter in the MPSGE model only targets model quantities. To target physical 
*	    quantities use the following formula which calculate phi as a function of the renewable target
*	    share, rest, and the manipulation by the report parameters.

*	NB: Currently, we can match REN share exactly only if non-renewables electricity
*	    only includes fossil, nuc, hydr. If we have electricity generated from advanced
*	    fossil backstops, the share is slightly off the target. This suggests that
*	    the formulation can be improved. 

phi_(r,t)$sren(r,t) = rest(r,t)/(1-rest(r,t)) * (stey0(r,"ele")-stelenuc(r)-stelehyd(r)+stelenuc(r)+stelehyd(r))
			/((edtot(r,"ele")/vtote(r,"ele"))*(vdm(r,"ele")
			+sum(trd,vxm(r,"ele",trd))+n_vdm(r)+sum(trd,n_vxm(r,trd))
			+h_vdm(r)+sum(trd,h_vxm(r,trd))));

*	National RPS instrument:

*	Activate RES from 2020 onward:
srenn(t)$(ord(t) ge 4) = 0;

*	Targets: 20% in 2020, 30% in 2030, 40% in 2040, 50% in 2050.
*	Set RES model instrument:

restn("2020") = 0.69;
restn("2025") = 0.75;
restn("2030") = 0.8;
restn("2035") = 0.82;
restn("2040") = 0.84;
restn("2045") = 0.82;
restn("2050") = 0.82;

* this time profile yields the following US shares for renewable electricity generation:
*2006 0.090,    2010 0.091,    2015 0.101,    2020 0.201,    2025 0.246,    2030 0.301,    2035 0.335,    2040 0.398
*2045 0.403,    2050 0.502
* and prenn.l is: 2020 2.981,    2025 4.174,    2030 5.068,    2035 4.156,    2040 3.071,    2045 1.265,    2050 0.896
 
* got this with flat 0.5:
*2006 0.090,    2010 0.091,    2015 0.142,    2020 0.153,    2025 0.168,    2030 0.191,    2035 0.221,    2040 0.229
*2045 0.217,    2050 0.229

phin_(t)$srenn(t) = restn(t);

*--------------------------------------------------------------------------
*	ACCOMMODATE SHALE GAS SUPPLY CONSISTENT WITH "MIT NG Study"
 
*	Expand base year rents for fixed factor in gas production: 

parameter gamma_rr(r,xe,t) "5-year growth factor for expanding fixed factor rents in fossil fuel production";

gamma_rr(r,xe,t) = 0;

parameter ramptarg	Ramp up period target;

* Ramp up until 2030:
$ontext
ramptarg = 6;
table gasrents(t,r)
	NEAST	SCENT	TX	MOUNT
2006	1	1	1	1
2010	1.61	1.24	1.07	1.04
2015	2.21	1.49	1.15	1.07
2020	2.82	1.73	1.22	1.11
2025	3.43	1.97	1.29	1.15
2030	4.03	2.22	1.37	1.18
;
$offtext

* Ramp up until 2025:
ramptarg = 5;
*$ontext
table gasrents(t,r)
	NEAST	SCENT	TX	MOUNT
2006	1	1	1	1
2010	1.76	1.30	1.09	1.05
2015	2.52	1.61	1.18	1.09
2020	3.27	1.91	1.27	1.14
2025	4.03	2.22	1.37	1.18
;
*$offtext

gamma_rr(r,"gas",t) = gasrents(t,r);

*	Switch off shale gas supply by setting "ramptarg=0":
ramptarg = 0;

*	Grow rents from crude oil extraction exogenously (otherwise we have
*	too much refined oil in the model)
parameter ramptarg_cru	Ramp up period target;
ramptarg_cru=10;
*$ontext
gamma_rr(r,"cru","2006") = 1;
gamma_rr(r,"cru","2010") = .9;
gamma_rr(r,"cru","2015") = .8;
gamma_rr(r,"cru","2020") = .7;
gamma_rr(r,"cru","2025") = .7;
gamma_rr(r,"cru","2030") = .6;
gamma_rr(r,"cru","2035") = .6;
gamma_rr(r,"cru","2040") = .5;
gamma_rr(r,"cru","2045") = .5;
gamma_rr(r,"cru","2050") = .5;
*$offtext

*	Regional markets for fossil fuel resources:
*	nmktf=0  regional markets
*	nmktf=1  national markets

nmktf = 1;



*--------------------------------------------------------------------------
*	TECHNOLOGY ASSUMPTIONS (EMF 24.2):

*	END-USE TECHNOLOGY - HIGH
*		[Low]: AEEI rates grow at 1% annually
*		[High]: AEEI rates grow at 1.5% annually

lamdae(r,s,t) = lamdae_(r,s,t);
lamdace(r,t) = lamdace_(r,t);

parameter
	ccsavailable	Flag to determine availability of CCS
	newnucavailable Flag to determine availability of new nuclear;


*	CCS - LOW
*		Low: CCS unavailable
*		High: CCS available

ccsavailable = no;

*	NUCLEAR - LOW	
*		[Low]:  Nuclear phase-out, no new nuclear available
*		[High]: Nuclear phase-out, new nuclear available

nuclphaseout = no;
newnucavailable = no;

*	High EOS bw nuclear resource and other inputs to meet capacity constraint. 

nsigma(r) = 0;

*	WIND & SOLAR - HIGH 
*		[Low]:  estimated markups
*		[High]: reduce markups for large-scale wind by x%.

badjst("windgas",r) = 0.95 * badjst("windgas",r);
badjst("windbio",r) = 0.95 * badjst("windbio",r);

*	BIOENERGY - HIGH
*		[Low]: estimated markups
*		[High]: reduce markups for bio-ele and bio-oil by x%.

*	default: 1.1
badjst("bioele",r) = 0.95 * badjst("bioele",r);
*	default: 3.1
badjst("bio-oil",r) = 0.95 * badjst("bio-oil",r);

*--------------------------------------------------------------------------
*	Specify GHG PRICING policy here:

set 
	ttco2c(t)	Identifier to activate tco2c flag in a certain period
	wwghgk(t)	Identifier to activate wghgk flag in a certain period
	tctaxf(r,t)	Identifier to activate ctaxf flag in a certain period
	taxco2c(r,t)	Identifier to activate co2c flag in a certain period
	taxghgk(r,t)	Identifier to activate ghgk flag in a certain period

;

*	Flag for tradable CO2 emission permits (activates "ptcarb"):
ttco2c(t)$(ord(t) ge 3) = 0;
*	Flag for tradable non-CO2 GHG emission permits (activates "pghgw"):
wwghgk(t)$(ord(t) ge 3) = 0;
*	GGH trading with CO2:
ghgt = 0;

tctaxf(r,t)$(ord(t) ge 3) = 0; 
taxco2c(r,t)$(ord(t) ge 3) = 0; 

*taxghgk(r,t)$(ord(t) ge 3) = 0; 
taxghgk(r,t)$((ord(t) ge 3)$(not sameas(r,"ak"))) = 0; 
*taxghgk("ca",t)$(ord(t) ge 3) = 0; 


Parameters
	reduc(t);
reduc(t) = 1/(1+.08)**ord(t);
*reduc(t) = 1;

*	Introduce GHG quotas:	
*cquota(r,t)$(ord(t) ge 3)= reduc(t)*co2_ref(t,r);
*gquota(ghg,r,t)$(ord(t) ge 3)= reduc(t)*ghg_ref(ghg,t,r);

*	Parameters for perfect foresight CO2e price path:
parameter 
	det_pr	Period in which to start a pre-determined carbon price increase
	p_ini	Initital carbon price for price increase
	int	Annual interest rate for price increase;

int = 0.04;
det_pr = 33;

*	Initial price (in $/1e2):
*       N.B.: price of carbon (10 billion $ / 100 million metric ton CO2) = CO2 emissions
*       in 100 million tons * 1e8 * 15$ * (1 /1e10)

p_ini = 44/1e4;

*	SECTORAL GHG PRICING:

set 
	sco2cnt(t)	Identifier to activate sco2cn flag in a certain period
	sghgwft(t)	Identifier to activate sghgwf in a certain period;

parameter 
	scarblimnt(s,t) Permits for sector s in t
	sghglimt(ghg,s,t) Permits for ghg in sector s in t;

*	Identify target sector:
ssn("trn") = no;

*	Reduction targets:
parameter reducscarbn(t) /
2015	1
2020	0.875111808
2025	0.774954887
2030	0.683098738
2035	0.598212849
2040	0.518721852
2045	0.445869951
2050	0.37871955
/;


*	CO2 permits:
sco2cnt(t)$(ord(t) ge 3) = no;
*scarblimnt(s,t)$ssn(s) = reducscarbn(t)*sum(r,co2s_ref(r,s,t));

*	GHG permits:
sghgwft(t)$(ord(t) ge 3) = no;
*sghglimt(ghg,s,t)$ssn(s) = reducscarbn(t)*sum(r,ghgs_ref(r,s,ghg,t));



*	ACTIVATE ALLOWANCE DISTRIBUTION:

*	Flag that turns on: 
*		- revenue-neutraliy constraint
*		- lumpsum transfers to households

parameter 
	lumpsumls_act(t) Activates lumpsumls;

*NB: This can only be active if there is an active carbon policy in the same period.

lumpsumls_act(t)$(ord(t) ge 3) = no;

*	Activate revenue-neutrality through lumpsum transfers:

rn_lst(t)$(ord(t) ge 3) = no;

*	Cost for international offsets:

offsetcost(t) = 0;

*	Determine allocation of carbon revenue. 
*	Here is we do a per capita rebate.

lshare_h(r,h) = popweight(r,h,"pop");

